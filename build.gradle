plugins {
    id "java"
    id "war"
    id "mx.com.inftel.wildfly" version "1.0.2"
    id "io.freefair.lombok" version "8.3"
    id "com.github.node-gradle.node" version "7.0.0"
}

group "ru.bardinpetr.itmo"
version "0.3"

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" }
}

ext {
    junit_ver = "5.9.1"
    arquillian_ver = "1.7.1.Final"
    shrinkwrap_ver = "3.2.1"
}

sourceCompatibility = "17"
targetCompatibility = "17"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    // JAKARTA 10 WEB
    implementation "jakarta.platform:jakarta.jakartaee-web-api:10.0.0"
    implementation "org.glassfish.web:web:10.0-b28"

    // WEB
    implementation "org.primefaces:primefaces:13.0.0:jakarta"
    implementation "org.jboss.weld.se:weld-se-core:5.1.2.Final"

    // validation
    implementation "org.hibernate.validator:hibernate-validator:7.0.5.Final"

    // JPA
    implementation "org.eclipse.persistence:org.eclipse.persistence.jpa:4.0.2"
    compileOnly "org.eclipse.persistence:eclipselink:4.0.2"

    // Logging
    implementation "org.slf4j:slf4j-api:2.0.7"
    runtimeOnly "ch.qos.logback:logback-core:1.4.7"
    runtimeOnly "ch.qos.logback:logback-classic:1.4.7"

    // JWT
    implementation "io.jsonwebtoken:jjwt-api:0.11.5"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.11.5"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.11.5"


    // Tests
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_ver}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_ver}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junit_ver}"
    testImplementation 'org.assertj:assertj-core:3.4.1'
    // testcontainers
    testImplementation "org.testcontainers:testcontainers:1.19.1"
    testImplementation "org.testcontainers:junit-jupiter:1.19.1"
    // postgres
    testImplementation "org.testcontainers:postgresql:1.19.1"
    testRuntimeOnly "org.postgresql:postgresql:42.6.0"
    // selenium
    testImplementation "org.testcontainers:selenium:1.19.1"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-remote-driver:3.141.59"
    // arquillian
    testImplementation "org.jboss.arquillian:arquillian-bom:${arquillian_ver}"
    testImplementation "org.jboss.arquillian.junit5:arquillian-junit5-container:${arquillian_ver}"
//    testImplementation "org.jboss.arquillian.junit:arquillian-junit-container:${arquillian_ver}"
    // arquillian containers
    testImplementation "org.wildfly.arquillian:wildfly-arquillian-container-remote:5.0.1.Final"
    testImplementation "org.jboss.arquillian.protocol:arquillian-protocol-servlet-jakarta:${arquillian_ver}"
    testImplementation "org.jboss.arquillian.container:arquillian-container-spi:${arquillian_ver}"
    testImplementation "org.jboss.arquillian.container:arquillian-container-test-spi:${arquillian_ver}"
    // dependency resolvers for war packing in runtime
    testImplementation "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-bom:${shrinkwrap_ver}"
    testImplementation "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:${shrinkwrap_ver}"
    testImplementation "org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-depchain:${shrinkwrap_ver}"
    implementation "org.gradle:gradle-tooling-api:7.3-20210825160000+0000" // because shrinkwrap has old gradle api
    testImplementation "org.dbunit:dbunit:2.7.3"
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
    }
}

wildfly {
    controller = "remote+http://localhost:9990"
    username = "admin"
    password = "1234"
    deployment = "build/libs/${rootProject.name}-${version}.war"
    persistent = false
}

node {
    nodeProjectDir.set(file("${project.projectDir}/src/main/webapp/src"))
}

//tasks.named("build") { finalizedBy("wildflyDeploy") }

tasks.named("wildflyDeploy") {
    dependsOn("build")
}

war {
    exclude("src/**")
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    dependsOn("yarn_build")
}